allprojects {
  test {
    include 'com/codeborne/selenide/**/*'
  }

  task chrome(type: Test) {
    systemProperties['selenide.browser'] = 'chrome'
    systemProperties['selenide.reportsFolder'] = 'build/reports/tests/chrome'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task chrome_headless(type: Test) {
    systemProperties['selenide.browser'] = 'chrome'
    systemProperties['selenide.headless'] = 'true'
    systemProperties['selenide.reportsFolder'] = 'build/reports/tests/chrome_headless'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  task chrome_remote(type: Test) {
    systemProperties['selenide.remote'] = 'http://localhost:4444/wd/hub'
    systemProperties['selenide.browser'] = 'chrome'
    systemProperties['selenide.reportsFolder'] = 'build/reports/tests/chrome_remote'
    include 'integration/**/*'
    exclude 'com/codeborne/selenide/**/*'
  }

  tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty('file.encoding', encoding)
    systemProperty('BUILD_URL', System.getenv()['BUILD_URL'])
    if (System.getenv('CI')) {
      maxParallelForks = 1
    } else {
      maxParallelForks = (int) Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
    jvmArgs = ['-ea', '-Xmx512m']
    systemProperties += System.properties
    systemProperties['user.language'] = 'FR'
  }
}

task allTests(dependsOn: ['clean', 'check', 'chrome_headless']) {}
